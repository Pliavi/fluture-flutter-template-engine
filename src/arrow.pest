// TODO: add implicity whitespace
// ref: https://pest.rs/book/grammars/syntax.html#implicit-whitespace
program = { SOI ~ widget ~ EOI }

widget = { WHITE_SPACE* ~ "widget" ~ WHITE_SPACE+ ~ typeIdent ~ WHITE_SPACE* ~ "{" ~ lineFeed* ~ body ~ "}" ~ lineFeed* }

body = { (WHITE_SPACE* ~ field ~ lineFeed*)* }

field = { attribute | stateField | providerField | selectField }

// positional attribute String? positionalWithDefault="Default value";
attributeScope = { "positional" }
attribute      = { (attributeScope ~ WHITE_SPACE+)? ~ "attribute" ~ WHITE_SPACE+ ~ typeIdent ~ WHITE_SPACE+ ~ ident ~ (WHITE_SPACE* ~ "=" ~ WHITE_SPACE* ~ expression)? ~ WHITE_SPACE* ~ ";" }

// state num counter=1;
stateFieldType = { "state" }
stateField     = { stateFieldType ~ WHITE_SPACE+ ~ typeIdent ~ WHITE_SPACE+ ~ ident ~ (WHITE_SPACE* ~ "=" ~ WHITE_SPACE* ~ expression)? ~ WHITE_SPACE* ~ ";" }

// read ApiProvider api;
// watch ProfileProvider profile;
providerFieldType = { "read" | "watch" }
providerField     = { providerFieldType ~ WHITE_SPACE+ ~ typeIdent ~ WHITE_SPACE+ ~ ident ~ WHITE_SPACE* ~ ";" }

// select Customer(customerName: name) CustomerProvider customer;
selectFieldType = { "select" }
selectRename    = { "{" ~ ident ~ ":" ~ WHITE_SPACE* ~ ident ~ "}" }
selectField     = { selectFieldType ~ WHITE_SPACE+ ~ typeIdent ~ WHITE_SPACE* ~ selectRename? ~ WHITE_SPACE+ ~ typeIdent ~ WHITE_SPACE+ ~ ident ~ WHITE_SPACE* ~ ";" }

ident     = { LETTER ~ ASCII_ALPHANUMERIC* }
typeIdent = { ident ~ "?"? }

expression = @{ string | number+ }
number     = @{ '0'..'9' }
string     = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

lineFeed = _{ "\r\n" | "\n" }
